import java.time.LocalDateTime
import java.time.format.DateTimeFormatter

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.8' apply false
    id 'io.spring.dependency-management' version '1.1.4'
}
jar {
    enabled = false
}
allprojects {
    group = 'org.limbusnoe'
    version = '1.0.0'

    repositories {
        mavenCentral()
    }
}
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '21'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        implementation 'org.projectlombok:lombok:1.18.38'
        annotationProcessor 'org.projectlombok:lombok:1.18.38'
    }
    bootBuildImage {
        imageName = "${project.name}:${version}"
        environment = [
                "BPE_DELIM_JAVA_TOOL_OPTIONS" : " ",
                "BPE_APPEND_JAVA_TOOL_OPTIONS" : "-Dspring.profiles.active=docker",
                "BP_JVM_VERSION" : "21"
        ]
        publish = false
        cleanCache = false
        verboseLogging = true
    }
    bootJar {
        archiveFileName = "${project.name}-${version}.jar"
    }
    jar {
        enabled = false
    }
    tasks.named('test') {
        useJUnitPlatform()
    }
}

tasks.register('cleanAll') {
    group = 'build'
    description = 'Cleans all projects'
    dependsOn subprojects.clean
    dependsOn clean
}

tasks.register('buildRelease') {
    group = 'build'
    description = 'Builds complete release with JARs and Docker images'

    dependsOn subprojects.build
    dependsOn subprojects.bootBuildImage
}

tasks.register('cleanBuildRelease') {
    group = 'build'
    description = 'Clean and build complete release'
    dependsOn cleanAll
    dependsOn buildRelease
}